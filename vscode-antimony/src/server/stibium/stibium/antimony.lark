
var_name : ["$"] NAME
in_comp : "in" var_name
namemaybein : var_name [in_comp]

empty:
reaction_name : namemaybein ":"
reaction : [reaction_name] species_list ARROW [species_list] ";" [sum] [in_comp]
    | [reaction_name] [species_list] ARROW species_list ";" [sum] [in_comp]
species_list : species ("+" species)*
species : [NUMBER] ["$"] NAME
ARROW : "->"
    | "=>"

interaction : [reaction_name] species INTERACTION_SYMBOL namemaybein
INTERACTION_SYMBOL : "-o" | "-|" | "-("

event : [reaction_name] "at" [event_delay] bool_exp [event_trigger_list] ":" event_assignment_list

event_delay : bool_exp "after"


event_trigger_list : ("," event_trigger)*

// atom has to be changed to (NUMBER | var_name)
event_trigger : "t0" "=" BOOLEAN
    | "priority" "=" sum
    | "fromTrigger" "=" BOOLEAN
    | "persistent" "=" BOOLEAN

event_assignment_list : event_assignment ("," event_assignment)*
event_assignment : var_name "=" sum

BOOLEAN : "true" | "false"

COMPARE : ">=" | "<=" | ">" | "<" | "=="


LOGICAL : "&&" | "||"

sboterm: var_name ".sboTerm" "=" SBOTERM
SBOTERM: NUMBER
    | "SBO:" NUMBER

assignment : namemaybein ("=" | AEQ) sum 

AEQ : ":="

apostrophe : "'"
rate_rule : NAME apostrophe "=" sum

annotation : var_name ANNOT_KEYWORD ESCAPED_STRING
ANNOT_KEYWORD: "identity"
    | "hasPart"
    | "biological_entity_is"
    | "parthood"
    | "part"
    | "isPartOf"
    | "isVersionOf"
    | "hypernym"
    | "hasVersion"
    | "version"
    | "isHomologTo"
    | "homolog"
    | "isDescribedBy"
    | "description"
    | "isEncodedBy"
    | "encoder"
    | "encodes"
    | "encodement"
    | "occursIn"
    | "container"
    | "hasProperty"
    | "propertyBearer"
    | "property"
    | "isPropertyOf"
    | "hasTaxon"
    | "taxon"


declaration : decl_modifiers decl_item ("," decl_item)*
decl_modifiers : VAR_MODIFIER
    | TYPE_MODIFIER
    | VAR_MODIFIER TYPE_MODIFIER
    | SUB_MODIFIER TYPE_MODIFIER
    | VAR_MODIFIER SUB_MODIFIER TYPE_MODIFIER
decl_item : namemaybein [decl_assignment]
decl_assignment : "=" sum

// unit
// builtin_unit : ("liter" | "mole" | "second" | "item" | "meter" | "liters" | "moles" | "seconds" | "items" | "meters")

unit : NAME

unit_declaration : "unit" var_name "=" sum

unit_assignment : var_name "has" sum

// this should not be a reaction name, I'm just being lazy
mmodel_call : [reaction_name] NAME "(" [parameters] ")"

function_call : namemaybein ("=" | AEQ) NAME "(" [parameters] ")"

VAR_MODIFIER: /\b(var|const)\b/
SUB_MODIFIER: /\b(substanceOnly)\b/
TYPE_MODIFIER: /\b(species|compartment|formula)\b/

?bool_exp : expressions
    | expressions (LOGICAL expressions)*

?expressions : sum
    | sum (COMPARE sum)*

?sum : product
    | sum "+" product
    | sum "-" product

?product : power
    | product "*" power
    | product "/" power

?power: atom
    | power "^" atom
    | "exp" atom

?atom : NUMBER
    | var_name
    | NUMBER var_name
    | "-" atom
    | "+" atom
    | "(" sum ")"
    | func_call 
    | "(" bool_exp ")"

func_call : var_name "(" [parameters] ")"

// optimizations possible when there are multiple empty lines
// example: https://docs.python.org/3/reference/grammar.html
// Now note that NEWLINE is already doing that, but ";" is not
?simple_stmt : [small_stmt] (";" | NEWLINE)
?small_stmt : reaction
    | assignment
    | declaration
    | annotation
    | unit_declaration
    | unit_assignment
    | mmodel_call
    | function_call
    | variable_in
    | is_assignment
    | import
    | interaction
    | rate_rule
    | sboterm
    | event

simple_stmt_list : simple_stmt+

// For some reason we need to have at least one suite in the model, even if it is just
// a newline or a semicolon
model : ("model" | "module") NAME simple_stmt_list END

import : "import" ESCAPED_STRING

// Modular Model
modular_model : "model" ["*"] NAME "(" [parameters] ")" simple_stmt_list END

// Function
function : "function" NAME "(" [parameters] ")" NEWLINE sum [";"] NEWLINE END

parameters : (NAME | NUMBER) ("," (NAME | NUMBER))*

variable_in : var_name in_comp

is_assignment : NAME "is" ESCAPED_STRING

END : "end"

COMMENT: "/*" /(.|\n)*/ "*/"
    | "#" /.*/
    | "//" /.*/


// TODO rename to 'file'
root : (simple_stmt | model | function | modular_model)*


// negative lookahead to avoid matching keywords (note the word boundaries)
// NAME : /(?!\b(var|const|compartment|species|formula|function|end|model|litre|mole|second|item|meter|litres|moles|seconds|"items"|"meters")\b)/ CNAME
NAME : /(?!\b(var|const|compartment|species|formula|function|end|model|substanceOnly)\b)/ CNAME

%import common.CNAME
%import common.NUMBER
%import common.WS
%import common.WS_INLINE
%import common.NEWLINE
%import common.ESCAPED_STRING
%import common.WORD
%import common.ESCAPED_STRING
%ignore WS_INLINE
%ignore COMMENT
